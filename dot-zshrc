# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
# Set the directory we want to store zinit and plugins
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"

# Download Zinit, if it's not there yet
if [ ! -d "$ZINIT_HOME" ]; then
   mkdir -p "$(dirname $ZINIT_HOME)"
   git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
fi

# Source/Load zinit
source "${ZINIT_HOME}/zinit.zsh"

# Add in Powerlevel10k
zinit ice depth=1; zinit light romkatv/powerlevel10k

# Load completions
autoload bashcompinit && bashcompinit
autoload -Uz compinit && compinit

# Add in zsh plugins
zinit light zsh-users/zsh-syntax-highlighting
zinit light zsh-users/zsh-completions
zinit light zsh-users/zsh-autosuggestions
zinit light Aloxaf/fzf-tab

# Add in snippets
zinit snippet OMZP::git
zinit snippet OMZP::sudo

# Load edit command line plugin
autoload -z edit-command-line
zle -N edit-command-line

# Export local path before loading autocompletions
export PATH="$PATH:$HOME/.local/bin"

## Load uv autocompletion if exists
if [[ -a /etc/bash_completion.d/uv ]]; then
  source /etc/bash_completion.d/uv
fi

## Load uvx autocompletion if exists
if [[ -a /etc/bash_completion.d/uvx ]]; then
  source /etc/bash_completion.d/uvx
fi

## Load rabbitmqadmin autocompletion if exists
if [[ -a /etc/bash_completion.d/rabbitmqadmin ]]; then
  source /etc/bash_completion.d/rabbitmqadmin
fi

## Load winconvpath if exists
if [[ -a ~/scripts/winpathconv.sh ]]; then
  source ~/scripts/winpathconv.sh
fi

complete -C '/usr/local/bin/aws_completer' aws
compdef _files aws

zinit cdreplay -q

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# Keybindings
bindkey -v
bindkey '^p' history-search-backward
bindkey '^n' history-search-forward
bindkey '^y' autosuggest-accept
bindkey '^e' edit-command-line

# History
HISTSIZE=5000
HISTFILE=~/.zsh_history
SAVEHIST=$HISTSIZE
HISTDUP=erase
setopt appendhistory
setopt sharehistory
setopt hist_ignore_space
setopt hist_ignore_all_dups
setopt hist_save_no_dups
setopt hist_ignore_dups
setopt hist_find_no_dups

# Completion styling
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*' menu no
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'ls --color $realpath'
zstyle ':fzf-tab:complete:__zoxide_z:*' fzf-preview 'ls --color $realpath'

# Aliases
alias ls='ls --color'
alias lla='ls -lah'
alias nvim='nvim'
alias c='clear'
alias aenv='source .venv/bin/activate || source venv/bin/activate'
alias cenv='python -m venv .venv'
alias denv='rm -r -I -v .venv'
alias dc='docker compose'
alias minimax='CLAUDE_CONFIG_DIR=~/.claude-minimax claude'
alias claude-account1="CLAUDE_CONFIG_DIR=~/.claude-account1 claude"

# Shell integrations (interactive shells only)
if [[ $- == *i* ]]; then
  eval "$(fzf --zsh)"
  eval "$(zoxide init --cmd cd zsh)"
  # Prefer fzf-tab over legacy fzf-completion binding on <Tab>
  if (( $+widgets[fzf-tab-complete] )); then
    bindkey '^I' fzf-tab-complete
  fi
fi

# Paths
export PATH="$PATH:/usr/local/go/bin"
export GOPATH=$HOME/go
export PATH="$PATH:$GOPATH/bin"

export NVM_DIR="$HOME/.nvm"
# Lazily load nvm to avoid console output during prompt init
load_nvm() {
  [ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh" --no-use
  [ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"
  if command -v nvm >/dev/null 2>&1; then
    local default_version
    default_version=$(nvm version default)
    if [ "$default_version" != "none" ] && [ "$default_version" != "N/A" ]; then
      nvm use --silent default >/dev/null
    fi
  fi
}

nvm() { unset -f nvm node npm npx; load_nvm; nvm "$@"; }
node() { unset -f nvm node npm npx; load_nvm; node "$@"; }
npm() { unset -f nvm node npm npx; load_nvm; npm "$@"; }
npx() { unset -f nvm node npm npx; load_nvm; npx "$@"; }

export PATH="$PATH:$HOME/.node/bin"

export PATH=$PATH:/home/jfj/.spicetify

# Set neovim as the default editor
export EDITOR=nvim

#THIS MUST BE AT THE END OF THE FILE FOR SDKMAN TO WORK!!!
export SDKMAN_DIR="$HOME/.sdkman"
[[ -s "$HOME/.sdkman/bin/sdkman-init.sh" ]] && source "$HOME/.sdkman/bin/sdkman-init.sh"

[[ ! -f ~/.local/bin/env ]] || . "$HOME/.local/bin/env"


# Load task scripts
[[ -f $HOME/.config/task/scripts/eisenhower.zsh ]] && source $HOME/.config/task/scripts/eisenhower.zsh

# Cargar todos los helpers de zsh
for f in $HOME/.config/zsh/*.zsh(N); do
  source $f
done


export PATH=/usr/local/bin:$PATH
